/*
 * Copyright 2017 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'

    id 'kotlin-android'
    id 'kotlin-kapt'
}

kapt {
    correctErrorTypes = true
    useBuildCache = true
}

def appVersionCode = propOrDef("APP_VERSIONCODE", 2) as Integer
println("APK version code: " + appVersionCode)

def useReleaseKeystore = rootProject.file("release/jimmy-release.keystore").exists()

ext {
    ci = System.getenv("CI") == "true"
}

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        applicationId "vn.mobifoneglobal.mhome"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode appVersionCode
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "JIMMY_API_KEY", "\"" + propOrDef("JIMMY_API_KEY", "") + "\""
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("release/jimmy-debug.keystore")
            storePassword "Ongthang00"
            keyAlias "jimmydebug"
            keyPassword "Ongthang00"
        }

        release {
            if (useReleaseKeystore) {
                storeFile rootProject.file("release/jimmy-release.keystore")
                storePassword propOrDef('JIMMY_APP_RELEASE', "")
                keyAlias "jimmy"
                keyPassword propOrDef('JIMMY_APP_RELEASE', "")
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    lintOptions {
        // Disable lintVital. Not needed since lint is run on CI
        checkReleaseBuilds false
        // Ignore any tests
        ignoreTestSources true
        // Make the build fail on any lint errors
        abortOnError true
        // Allow lint to check dependencies
        checkDependencies true
    }

    buildFeatures {
        compose true
        viewBinding true
        dataBinding true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composecompiler.get()
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    packagingOptions {
        // Exclude AndroidX version files
        exclude 'META-INF/*.version'
        // Exclude consumer proguard files
        exclude 'META-INF/proguard/*'
        // Exclude the Firebase/Fabric/other random properties files
        exclude '/*.properties'
        exclude 'fabric/*.properties'
        exclude 'META-INF/*.properties'
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix "-dev"
            applicationIdSuffix ".debug"
        }

        release {
            if (useReleaseKeystore) {
                signingConfig signingConfigs.release
            } else {
                // Otherwise just use the debug keystore (this is mainly for PR CI builds)
                signingConfig signingConfigs.debug
            }
            shrinkResources true
            minifyEnabled true
            proguardFiles(
                    getDefaultProguardFile("proguard-android.txt"),
                    "proguard-rules.pro"
            )
        }
    }

    flavorDimensions "mode"
    productFlavors {
        qa {
            dimension "mode"
            // This is a build with Chucker enabled
            proguardFiles += 'proguard-rules-chucker.pro'
            versionNameSuffix "-qa"
        }

        standard {
            dimension "mode"
            // Standard build is always ahead of the QA builds as it goes straight to
            // the alpha channel. This is the 'release' flavour
            versionCode android.defaultConfig.versionCode + 1
        }
    }

    variantFilter { variant ->
        // Ignore the standardDebug variant, QA == debug
        def isQa = variant.flavors.any { it.name.contains("qa") }
        def isDebug = variant.buildType.name == "debug"
        if (!isQa && isDebug) {
            setIgnore(true)
        }
    }
}

dependencies {

    implementation project(':base')
    implementation project(':base-android')
    implementation project(':common-ui-view')
    implementation project(':common-imageloading')
    implementation project(':common-ui-compose')
    implementation project(':common-ui-resources')
    implementation project(':data')
    implementation project(':data-android')
    implementation project(':domain')
    implementation project(':ui-login')
    implementation project(':ui-register')
    implementation project(':ui-settings')
    implementation project(':ui-forgotpassword')
    implementation project(':ui-home')
    implementation project(':ui-cameradetails')
    implementation project(':ui-playback')
    implementation project(':ui-player')
    implementation project(':ui-album')
    implementation project(':ui-add-device')
    implementation project(':ui-qrscan')
    implementation project(':tasks')


    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.activity.compose
    implementation libs.androidx.navigation.compose
    implementation libs.androidx.emoji

    implementation libs.accompanist.insets
    implementation libs.accompanist.insetsui
    implementation libs.accompanist.navigation.animation
    implementation libs.accompanist.permissions
    implementation libs.accompanist.systemuicontroller

    implementation libs.compose.foundation.foundation
    implementation libs.compose.foundation.layout
    implementation libs.compose.material.material
    implementation libs.compose.material.iconsext
    implementation libs.compose.animation.animation
    implementation libs.compose.ui.tooling

    implementation libs.datastore


    implementation libs.google.crashlytics
    implementation libs.google.analytics
    implementation libs.google.messaging


    implementation libs.hilt.library
    implementation libs.androidx.hilt.compose
    kapt libs.hilt.compiler
    implementation libs.androidx.hilt.work
    kapt libs.androidx.hilt.compiler


    implementation libs.kotlin.stdlib
    implementation libs.kotlin.coroutines.core
    implementation libs.kotlin.coroutines.android
    implementation libs.material
    implementation libs.material3
    implementation libs.splash.screen
    implementation libs.retrofit.gsonConverter
    implementation libs.threeTenAbp
    implementation libs.timber

    implementation "com.github.fornewid.material-motion-compose:core:0.9.0-alpha08"

    qaImplementation libs.chucker.library
    qaImplementation libs.debugdrawer.debugdrawer
    qaImplementation libs.debugdrawer.leakcanary
    qaImplementation libs.debugdrawer.retrofit
    qaImplementation libs.retrofit.mock
    qaImplementation libs.debugdrawer.timber
    qaImplementation libs.debugdrawer.okhttplogger
    qaImplementation libs.leakCanary

    testImplementation libs.junit
    testImplementation libs.robolectric
    testImplementation libs.androidx.test.core
    testImplementation libs.androidx.test.rules
}

android.applicationVariants.all { variant ->
    task "open${variant.name.capitalize()}" {
        dependsOn "install${variant.name.capitalize()}"

        doLast {
            exec {
                commandLine "adb shell monkey -p ${variant.applicationId} -c android.intent.category.LAUNCHER 1".split(" ")
            }
        }
    }
}

if (file("google-services.json").exists()) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}
